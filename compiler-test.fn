
(

;; Variable environments
(->true
 (def %xyz-env (global-env 'linked-with-vars (list 'x 'y 'z)))
 (def %abz-env (%xyz-env 'linked-with-vars (list 'a 'b 'z))))

(string= "GLOBAL" (send global-env 'prettyprint))
(string= "(x y z)::GLOBAL" (send %xyz-env 'prettyprint))

(eql '((read-var 0 0)) (send %xyz-env 'generate-read 'x))
(eql '((read-var 0 1)) (send %xyz-env 'generate-read 'y))
(eql '((read-var 0 2)) (send %xyz-env 'generate-read 'z))
(eql '((read-global-var foo)) (send %xyz-env 'generate-read 'foo))

(eql '((read-var 0 0)) (send %abz-env 'generate-read 'a))
(eql '((read-var 0 1)) (send %abz-env 'generate-read 'b))
(eql '((read-var 0 2)) (send %abz-env 'generate-read 'z)) ; Shadows z in xyz-env
(eql '((read-var 1 0)) (send %abz-env 'generate-read 'x))
(eql '((read-var 1 1)) (send %abz-env 'generate-read 'y))

(eql '((write-var 1 1)) (send %abz-env 'generate-write 'y))
(eql '((write-var 0 2)) (send %abz-env 'generate-write 'z))

(->true
 ;; Predictable version of compile
 ;; where the generated gensyms are always the same.
 (defn pcompile (expr env)
   (set! global-label-counter 1000)
   (compile expr env 'call)))

(eql '((load-value 123)
       (push))
     (pcompile 123 global-env))

(eql '((load-value #\a)
       (push))
     (pcompile #\a global-env))

(eql '((load-value (+ 1 2 3))
       (push))
     (pcompile '(quote (+ 1 2 3)) global-env))

(eql '((read-var 0 0)  ; first var
       (load-value 42)
       (push)
       (load-value 99)
       (push)
       (call 3)
       (push))  ; 2 arguments and one procedure.
     (pcompile '(x 42 99) %xyz-env))

(eql '((read-var 0 0)
       (pop)
       (jump-if-true true-branch-1001)
       ; False branch
       (load-value 0)
       (push)
       (jump after-if-1002)
       ; True branch
       (label true-branch-1001)
       (load-value 10)
       (push)
       (label after-if-1002))
     (pcompile '(if x 10 0) %xyz-env))

(eql '((make-lambda lambda-entry-1001 (x))
       (push)
       (jump after-lambda-1002)
       ; -- this is where the lambda starts
       (label lambda-entry-1001)
       (read-global-var +)
       (read-var 0 0)  ; x
       (read-global-var +)
       (read-var 1 1)  ; b
       (read-var 1 1)  ; b
       (call 3)  ; 2 arguments and one procedure.
       (push)
       (tail-call 3)  ; 2 arguments and one procedure.
       (push)
       (pop)
       (return)
       ;; -- this is where the lambda ends
       (label after-lambda-1002))
     (pcompile '(lambda (x) (+ x (+ b b))) %abz-env))

(eql '((load-value 5)
       (push)
       (pop)
       (write-global-var foo)
       (push))
     (pcompile '(def foo 5) %xyz-env))

(eql '((lambda (x y) (+ x y)) 1 2)
     (transform-let->lambda '(let ((x 1) (y 2)) (+ x y))))

(eql (pcompile '((lambda (x y) (+ x y)) 1 2) global-env)
     (pcompile '(let ((x 1) (y 2)) (+ x y)) global-env))

(eql '((load-value 42)
       (push)
       (pop)
       (write-global-var meaning-of-life)
       (push))
     (pcompile '(set! meaning-of-life 42) global-env))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Tests for byte stream.                                  ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(progn
  (def %buf (make-byte-stream))
  (= 0 (send %buf 'size)))

(progn
  (send %buf 'put-byte! 0)
  (send %buf 'put-byte! 1)
  (send %buf 'put-byte! 2)
  (send %buf 'put-byte! 3)
  (send %buf 'put-byte! 4)
  (send %buf 'put-byte! 5)
  (send %buf 'put-byte! 6)
  (send %buf 'put-byte-at! 2 20)
  (eql '(0 1 20 3 4 5 6)
       (send %buf 'slice 0 7)))

(= 7 (send %buf 'size))

(= 57 (lower-byte 12345))
(= 48 (upper-byte 12345))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Execute some real compiled functions!                    ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(eql (cons 1 2)
     ((compile-fn cons) 1 2))

(eql (list 1 2 3)
     ((compile-fn map) identity (list 1 2 3)))

(string= "hello"
         ((compile-fn (lambda () "hello"))))

;; It works!
(progn
  (set! map (compile-fn map))
  (eql '(97 98 99)
       (map char->num (list #\a #\b #\c))))

(progn
  ((compile-fn (lambda (x) "xx" (def __just_a_test x))) 99)
  (= 99 __just_a_test))

)
