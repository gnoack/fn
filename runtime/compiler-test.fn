;; Variable environments
(->true
 (def %xyz-env (send global-env 'linked-with-vars (list 'x 'y 'z)))
 (def %abz-env (send %xyz-env 'linked-with-vars (list 'a 'b 'z))))

(string=? "GLOBAL" (send global-env 'prettyprint))
(string=? "(x y z)::GLOBAL" (send %xyz-env 'prettyprint))

(eql? '((read-var 0 0)) (send %xyz-env 'generate-read 'x))
(eql? '((read-var 0 1)) (send %xyz-env 'generate-read 'y))
(eql? '((read-var 0 2)) (send %xyz-env 'generate-read 'z))
(eql? '((read-global-var foo)) (send %xyz-env 'generate-read 'foo))

(eql? '((read-var 0 0)) (send %abz-env 'generate-read 'a))
(eql? '((read-var 0 1)) (send %abz-env 'generate-read 'b))
(eql? '((read-var 0 2)) (send %abz-env 'generate-read 'z)) ; Shadows z in xyz-env
(eql? '((read-var 1 0)) (send %abz-env 'generate-read 'x))
(eql? '((read-var 1 1)) (send %abz-env 'generate-read 'y))

(eql? '((write-var 1 1)) (send %abz-env 'generate-write 'y))
(eql? '((write-var 0 2)) (send %abz-env 'generate-write 'z))

(->true
 ;; Predictable version of compile
 ;; where the generated gensyms are always the same.
 (defn pcompile (expr env)
   (with-gensym-scope
    (compile expr env 'call))))

(eql? '((load-value 123))
      (pcompile 123 global-env))

(eql? '((load-value #\a))
      (pcompile #\a global-env))

(eql? '((load-value (+ 1 2 3)))
      (pcompile '(quote (+ 1 2 3)) global-env))

(eql? '((read-var 0 0)  ; first var
        (load-value 42)
        (load-value 99)
        (call 3))  ; 2 arguments and one procedure.
      (pcompile '(x 42 99) %xyz-env))

(eql? '((read-var 0 0)
        (jump-if-true true-branch-1)
        ;; False branch
        (load-value 0)
        (jump after-if-2)
        ;; True branch
        (label true-branch-1)
        (load-value 10)
        (label after-if-2))
      (pcompile '(if x 10 0) %xyz-env))

(eql? '((make-lambda lambda-entry-1 5 (x))
        (jump after-lambda-2)
        ;; -- this is where the lambda starts
        (label lambda-entry-1)
        (read-global-var +)
        (read-var 0 0)  ; x
        (read-global-var +)
        (read-var 1 1)  ; b
        (read-var 1 1)  ; b
        (call 3)        ; 2 arguments and one procedure.
        (tail-call 3)   ; 2 arguments and one procedure.
        (return)
        ;; -- this is where the lambda ends
        (label after-lambda-2))
      (pcompile '(lambda (x) (+ x (+ b b))) %abz-env))

(eql? '((load-value 5)
        (write-global-var foo))
      (pcompile '(def foo 5) %xyz-env))

(eql? '((lambda (x y) (+ x y)) 1 2)
      (transform-let->lambda '(let ((x 1) (y 2)) (+ x y))))

(eql? (pcompile '((lambda (x y) (+ x y)) 1 2) global-env)
      (pcompile '(let ((x 1) (y 2)) (+ x y)) global-env))

(eql? '((load-value 42)
        (write-global-var meaning-of-life))
      (pcompile '(set! meaning-of-life 42) global-env))

(= 57 (lower-byte 12345))
(= 48 (upper-byte 12345))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Execute some real compiled functions!                    ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(eql? (cons 1 2)
      ((compile-fn cons) 1 2))

(eql? (list 1 2 3)
      ((compile-fn map) identity (list 1 2 3)))

(string=? "hello"
          ((compile-fn (lambda () "hello"))))

;; It works!
(progn
  (set! map (compile-fn map))
  (eql? '(97 98 99)
        (map char->num (list #\a #\b #\c))))

(progn
  ((compile-fn (lambda (x) "xx" (def __just_a_test x))) 99)
  (= 99 __just_a_test))

;; Maximum stack depth.
(eq? 1 (max-stack-depth 99))
(eq? 1 (max-stack-depth #\a))
(eq? 1 (max-stack-depth 'foo))
(eq? 1 (max-stack-depth "foo"))
(eq? 1 (max-stack-depth '(if 1 2 3)))
(eq? 4 (max-stack-depth '(+ 1 2 3)))
(eq? 5 (max-stack-depth '(+ (+ 1 2 3)
                            (+ 1 2)
                            (+ 1)
                            1)))
(eq? 3 (max-stack-depth '(if true (+ 1 2) 0)))
(eq? 3 (max-stack-depth '(if true 0 (+ 1 2))))
(eq? 1 (max-stack-depth '(lambda (x) (+ 1 2 3 4 5 x))))
(eq? (max-stack-depth '(let ((a 1) (b 2)) 'foo))
     (max-stack-depth '((lambda (a b) 'foo) 1 2)))
(eq? 3 (max-stack-depth '(def x (+ 1 2))))
(eq? 3 (max-stack-depth '(set! x (+ 1 2))))
(eq? 3 (max-stack-depth '(progn 1 2 (+ 3 4) 5)))
(eq? 1 (max-stack-depth '(progn 1 2 3 4 5)))
