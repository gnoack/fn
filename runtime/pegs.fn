;; Implements parsing expression grammars and the `defgrammar` macro.
;;
;; PEGs are transcribed from the paper
;; 'Parsing Expression Grammars' by Bryan Ford.
;;
;; A parsing expression grammar is a function with arguments
;; (input win lose) that parses the input, where:
;;
;;  `input' is a list of objects (e.g. characters, but anything is possible)
;;  `win'   is the success function, invoked with (win result remainder)
;;          where `result' is the parse result
;;            and `remainder' is the remaining input
;;  `lose'  is the failure function, invoked with (lose remainder ERR*)
;;          where `remainder' is the input where it failed
;;            and `ERR*' are more arguments describing the error

(defn empty? (x)
  (nil? x))

(def peg-empty
    (lambda (input win lose)
      (if (empty? input)
	  (win '() nil)
        (lose input "Empty input expected."))))

(defn peg-terminal (predicate? displayable-name)
  (lambda (input win lose)
    (if (empty? input)
	(lose input "Wanted input, got nothing.")
	(let ((x (first input)))
	  (if (predicate? x)
	      (win x (rest input))
            (lose input 'expected displayable-name 'got x))))))

;; We assume peg2 yields a list as win result.
(defn peg-seq2 (peg1 peg2)
  (lambda (input win lose)
    (peg1 input
	  (lambda (result1 rest1)
	    (peg2 rest1
		  (lambda (result2 rest2)
		    (win (cons result1 result2)
			 rest2))
		  lose))
	  lose)))

(defn peg-winseq (input win lose)
  (win '() input))

(defn peg-alwaysfail (input win lose)
  (lose input "Fail."))

(defn peg-seq (&rest ps)
  (reduce-right peg-seq2
		ps
		peg-winseq))

(defn peg-alt2 (peg1 peg2)
  (lambda (input win lose)
    (peg1 input
	  win
	  (lambda (remainder &rest reason)
	    (peg2 input win lose)))))

(defn peg-alt (&rest ps)
  (reduce-right peg-alt2
		ps
		peg-alwaysfail))

(defn peg*er (peg input win lose)
  (peg input
       (lambda (result rest)
	 (peg*er peg
		  rest
		  (lambda (result+ rest+)
		    (win (cons result result+)
			 rest+))
		  (lambda (&rest failure-reason)
		    (win (list result) rest))))
       (lambda (&rest failure-reason)
	 (win '() input))))

; Matches 0 or more sequential occurrences of peg.
(defn peg* (peg)
  (lambda (input win lose)
    (peg*er peg input win lose)))

; Matches 1 or more sequential occurrences of peg.
(defn peg+ (peg)
  (peg-seq2 peg (peg* peg)))

(defn peg-not (peg)
  (lambda (input win lose)
    (peg input
	 (lambda (result rest)
	   (lose input "Should not match."))
	 (lambda (&rest failure-reason)
	   (win nil input)))))

; Result transformation.
(defn peg=> (peg transform)
  (lambda (input win lose)
    (peg input
	 (lambda (result rest)
	   (win (transform result) rest))
	 lose)))

(defn applicator (fun)
  (lambda (arglist)
    (apply fun arglist)))

(defmacro peg-let (bindings action)
  `(peg=> (peg-seq ,@(map second bindings))
          (applicator (lambda ,(map first bindings)
                        ,action))))

;; PEG utilities
(defn parses? (peg input)
  (peg input ->true ->false))

(defn parse-fail? (peg input)
  (not (parses? peg input)))

(defn print-parse-error-and-raise (remainder &rest reason)
  (println "*PARSE ERROR")
  (println "At: " (pprint (list->string (take 50 remainder))))
  (println "     ^")
  (raise reason))


;;; The following macros are used for `defgrammar`.

;; Indirect lookup for PEG rules to resolve circular dependencies.
;;
;; (peg-indirect R)     => R        ; through indirect with same args
;; (peg-indirect R a b) => (R a b)  ; invoke high-level PEG
;;
;; The result of peg-indirect should always be a PEG
;; (a function with the arguments input win lose).
(defmacro peg-indirect (name &rest args)
  (if (nil? args)
      `(lambda (input win lose)
         (,name input win lose))
    `(,name ,@args)))

;; Shortcut for (def ...) in grammar definitions for consistent syntax
;; for basic and high-level rules (a.k.a. functions).
;;
;; (defrule name ()   peg)       => (def name peg)
;; (defrule name (x)  (mypeg x)) => (defn name (x) (mypeg x))
(defmacro defrule (name params peg)
  (if (nil? params)
      `(def ,name ,peg)
    `(defn ,name ,params ,peg)))

;; Define the grammar under the given name.
;;
;; Import clauses are as specified in modules.fn,
;; ex: (MODULE a b c) to import a, b and c.
;; ex: (MODULE (my-a a) (my-b b)) to import a, b as my-a, my-b.
;;
;; The rules should macroexpand to (def x ...) clauses, which
;; are normally defined with the (defrule x () ...) helper above
;; for uniformity.
(defmacro defgrammar (name import-clauses &rest rules)
  ;; Rules are (def name rule)
  (let ((rules (map macroexpand rules)))
    `(def ,name
       (lambda ,(map first import-clauses)
         (import ,import-clauses
           ,@rules
           (make-module ,@(map second rules)))))))
