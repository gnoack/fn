(def *assertions-executed* 0)

;; TODO: Cache compiled tests, not just expressions.
;; We are currently caching expressions, because all expressions
;; are being wrapped in (assert) in the tests.
(defn read-cached (filename)
  (let ((cache-filename (string-append filename ".exprc")))
    (catch (lambda () (deserialize-from-file! cache-filename))
      (lambda (reason)
        (let ((expressions (read-all (file->string filename))))
          (serialize-to-file! cache-filename expressions)
          expressions)))))

(defn run-test-file (filename)
  (dolist (item (read-cached filename))
    (eval `(assert ,item))
    (inc! *assertions-executed*)
    (writeout "."))
  (println))

(defn run-all-tests (filenames)
  (dolist (filename filenames)
    (writeout filename)
    (writeout ": ")
    (run-test-file filename))

  (println *assertions-executed* " assertions executed, 0 failures.")
  (println "Well done!"))

(when (empty? *args*)
  (error "You need to specify at least one file."))

(run-all-tests *args*)
